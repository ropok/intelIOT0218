void print_menu() {
  Serial.println(F("-------------------------------------"));
  Serial.println(F("[?] Print this menu"));

  // Customised
  Serial.println(F("[0] Send a dht11 reading via sms"));
  Serial.println(F("[1] Send a gps reading via sms"));
  Serial.println(F("[2] Send a dht11 reading via Thingspeak"));
  Serial.println(F("[3] Send a gps reading via Thingspeak"));

  // SMS
  Serial.println(F("[s] Send SMS"));

  // Time
  Serial.println(F("[t] Get network time"));

  // GPRS
  //  Serial.println(F("[G] Enable GPRS"));
  //  Serial.println(F("[g] Disable GPRS"));
  //  Serial.println(F("[l] Query GSMLOC (GPRS)"));
  //  Serial.println(F("[w] Read webpage (GPRS)"));
  //  Serial.println(F("[W] Post to website (GPRS)"));

  // GPS
  Serial.println(F("[O] Turn GPS on"));
  Serial.println(F("[o] Turn GPS off"));
  Serial.println(F("[x] Query GPS status"));
  Serial.println(F("[L] Query GPS location"));
  Serial.println(F("-------------------------------------"));
  Serial.println(F(""));
}

void run_menu() {
  Serial.print(F("FONA> "));
  while (! Serial.available() ) {
    if (fona.available()) {
      Serial.write(fona.read());
    }
  }

  char command = Serial.read();
  Serial.println(command);

  switch (command) {
    case '?': {
        print_menu();
        break;
      }

    /*** Customised ***/
    case '0': {
        //Send a dht11 reading via sms
        String toSendString = get_a_dht_reading();
        Serial.println(F("Sending a DHT11 reading via sms ... "));
        send_sms(myMobileNum, toSendString);
        break;
      }

    case '1': {
        //Send a gps reading via sms
        String myString = googleMapsQueryString + get_a_gps_reading();
        Serial.println(F("Sending a GPS reading via sms ... "));
        send_sms(myMobileNum, myString );
        break;
      }

    case '2': {
        //Send a dht11 reading via Thingspeak
        get_a_dht_reading();
        Serial.println(F("Sending a DHT11 reading via ThingSpeak ..."));
        send_thingspeak_3g(dht_temp, dht_humid);
        break;
      }

    case '3': {
        //Send a gps reading via Thingspeak
        get_a_gps_reading();
        Serial.println(F("Sending a GPS reading via ThingSpeak ..."));
        send_thingspeak_3g(gps_lat, gps_lon);
        break;
      }

    /*** SMS ***/

    case 's': {
        // send an SMS!
        char sendto[21], message[141];
        flushSerial();
        Serial.print(F("Send to #"));
        readline(sendto, 20);
        Serial.println(sendto);
        Serial.print(F("Type out one-line message (140 char): "));
        readline(message, 140);
        Serial.println(message);
        if (!fona.sendSMS(sendto, message)) {
          Serial.println(F("Failed"));
        } else {
          Serial.println(F("Sent!"));
        }

        break;
      }

    /*** Time ***/

    case 't': {
        // read the time
        char buffer[23];

        fona.getTime(buffer, 23);  // make sure replybuffer is at least 23 bytes!
        Serial.print(F("Time = ")); Serial.println(buffer);
        break;
      }

    /************ GPS ***********/

    case 'o': {
        // turn GPS off
        if (!fona.enableGPS(false))
          Serial.println(F("Failed to turn off"));
        break;
      }
    case 'O': {
        // turn GPS on
        if (!fona.enableGPS(true))
          Serial.println(F("Failed to turn on"));
        break;
      }
    case 'x': {
        int8_t stat;
        // check GPS fix
        stat = fona.GPSstatus();
        if (stat < 0)
          Serial.println(F("Failed to query"));
        if (stat == 0) Serial.println(F("GPS off"));
        if (stat == 1) Serial.println(F("No fix"));
        if (stat == 2) Serial.println(F("2D fix"));
        if (stat == 3) Serial.println(F("3D fix"));
        break;
      }

    case 'L': {
        // check for GPS location
        char gpsdata[120];
        fona.getGPS(0, gpsdata, 120);
        //        if (type == FONA808_V1)
        Serial.println(F("Reply in format: longitude, latitude, UTC date(yyyymmdd), UTC time(HHMMSS), altitude, speed, course"));
        //        else
        //          Serial.println(F("Reply in format: mode,fixstatus,utctime(yyyymmddHHMMSS),latitude,longitude,altitude,speed,course,fixmode,reserved1,HDOP,PDOP,VDOP,reserved2,view_satellites,used_satellites,reserved3,C/N0max,HPA,VPA"));
        Serial.println(gpsdata);

        break;
      }

    /********* default ***********/
    default: {
        Serial.println(F("Unknown command"));
        print_menu();
        break;
      }
  }

  // flush input
  flushSerial();
  while (fona.available()) {
    Serial.write(fona.read());
  }
}

void flushSerial() {
  while (Serial.available())
    Serial.read();
}

char readBlocking() {
  while (!Serial.available());
  return Serial.read();
}
uint16_t readnumber() {
  uint16_t x = 0;
  char c;
  while (! isdigit(c = readBlocking())) {
    //Serial.print(c);
  }
  Serial.print(c);
  x = c - '0';
  while (isdigit(c = readBlocking())) {
    Serial.print(c);
    x *= 10;
    x += c - '0';
  }
  return x;
}

uint8_t readline(char *buff, uint8_t maxbuff, uint16_t timeout) {
  uint16_t buffidx = 0;
  boolean timeoutvalid = true;
  if (timeout == 0) timeoutvalid = false;

  while (true) {
    if (buffidx > maxbuff) {
      //Serial.println(F("SPACE"));
      break;
    }

    while (Serial.available()) {
      char c =  Serial.read();

      //Serial.print(c, HEX); Serial.print("#"); Serial.println(c);

      if (c == '\r') continue;
      if (c == 0xA) {
        if (buffidx == 0)   // the first 0x0A is ignored
          continue;

        timeout = 0;         // the second 0x0A is the end of the line
        timeoutvalid = true;
        break;
      }
      buff[buffidx] = c;
      buffidx++;
    }

    if (timeoutvalid && timeout == 0) {
      //Serial.println(F("TIMEOUT"));
      break;
    }
    delay(1);
  }
  buff[buffidx] = 0;  // null term
  return buffidx;
}

